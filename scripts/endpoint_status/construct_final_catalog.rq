PREFIX schema: <https://schema.org/> 
PREFIX dcat: <http://www.w3.org/ns/dcat#> 
PREFIX dcterms: <http://purl.org/dc/terms/> 
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav: <http://purl.org/pav/>
PREFIX kgi: <http://ns.inria.fr/kg/index#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>

CONSTRUCT {
    kgi:catalog a dcat:Catalog ;
        dcat:service ?dataset .
    ?dataset a sd:Service , dcat:DataService ;
        dcat:endpointURL ?e ;
        sd:endpoint ?e ;
        schema:serverStatus ?status ;
        schema:dateModified ?testedTime ;
        dcterms:available ?maxTime ;
        pav:sourceAccessedOn ?minTime ;
        pav:sourceLastAccessedOn ?maxTime .
}
{
    VALUES ?endpointProperty { sd:endpoint void:sparqlEndpoint dcat:endpointURL }
    VALUES ?timeProperty { schema:dateModified dcterms:available pav:sourceAccessedOn pav:sourceLastAccessedOn }

    ?s ?endpointProperty ?e ;
        schema:serverStatus ?status .
    
    OPTIONAL {
        {
            SELECT ?e ?minTime {
                VALUES ?timeProperty { dcterms:available pav:sourceAccessedOn pav:sourceLastAccessedOn }
                VALUES ?endpointProperty { sd:endpoint void:sparqlEndpoint dcat:endpointURL }
                ?s ?endpointProperty ?e ;
                    ?timeProperty ?minTime .
                FILTER( NOT EXISTS { 
                    ?s ?endpointProperty ?e ;
                        ?timeProperty ?time . 
                    FILTER( ?time < ?minTime )
                } )
            }
        }
    }
    OPTIONAL {
        {
            SELECT ?e ?maxTime {
                VALUES ?timeProperty { schema:dateModified dcterms:available pav:sourceAccessedOn pav:sourceLastAccessedOn }
                VALUES ?endpointProperty { sd:endpoint void:sparqlEndpoint dcat:endpointURL }
                ?s ?endpointProperty ?e ;
                    ?timeProperty ?maxTime ;
                    schema:serverStatus schema:Online .
                FILTER( NOT EXISTS { 
                    ?s ?endpointProperty ?e ;
                        ?timeProperty ?time . 
                    FILTER( ?time > ?maxTime )
                } )
            }
        }
    }
    OPTIONAL {
        {
            SELECT ?e ?testedTime {
                VALUES ?timeProperty { schema:dateModified }
                VALUES ?endpointProperty { sd:endpoint void:sparqlEndpoint dcat:endpointURL }
                ?s ?endpointProperty ?e ;
                    ?timeProperty ?testedTime .
                FILTER( NOT EXISTS { 
                    ?s ?endpointProperty ?e ;
                        ?timeProperty ?time . 
                    FILTER( ?time > ?testedTime )
                } )
            }
        }
    }
    BIND( IRI( CONCAT( STR(kgi:), ENCODE_FOR_URI( STR( ?e ) ) ) ) AS ?dataset )
}